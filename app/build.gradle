plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.0'
    id 'org.jetbrains.kotlin.android'
}

//https://devdeeds.com/auto-increment-build-number-using-gradle-in-android/
def versionPropsFile = file('version.properties')
def versionBuild

/*Setting default value for versionBuild which is the last incremented value stored in the file */
if (versionPropsFile.canRead()) {
    Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))
    versionBuild = versionProps['VERSION_BUILD'].toInteger()
} else {
    throw new FileNotFoundException("Could not read version.properties!")
}

/*Wrapping inside a method avoids auto incrementing on every gradle task run. Now it runs only when we build apk*/
ext.autoIncrementBuildNumber = {
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
    } else {
        throw new FileNotFoundException("Could not read version.properties!")
    }
}

// Hook to check if the release/debug task is among the tasks to be executed.
//Let's make use of it
gradle.taskGraph.whenReady {taskGraph ->
    if (taskGraph.hasTask(assembleDebug)) {  /* when run debug task */
        autoIncrementBuildNumber()
    } else if (taskGraph.hasTask(assembleRelease)) { /* when run release task */
        autoIncrementBuildNumber()
    }
}

android {
    compileSdk 35

    defaultConfig {
        applicationId "eu.ydiaeresis.trovalasonda"
        minSdkVersion 21
        targetSdkVersion 35

        versionCode 110000+versionBuild
        versionName "1.1.0."+versionBuild
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
    namespace 'eu.ydiaeresis.trovalasonda'
}

dependencies {
    implementation(platform('org.jetbrains.kotlin:kotlin-bom:2.1.0'))
    implementation 'com.github.deano2390:MaterialShowcaseView:1.3.7'
    implementation 'com.google.android.datatransport:transport-runtime:4.0.0'
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.webkit:webkit:1.12.1'
    implementation 'androidx.core:core-ktx:1.15.0'
    kapt "com.android.databinding:compiler:3.1.4"
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.1.4")
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation files('../../android-bluetooth-serial/androidBluetoothSerial/build/outputs/aar/androidBluetoothSerial-release.aar')
    //implementation 'com.github.buttim:android-bluetooth-serial:1.1.7'
    implementation 'org.osmdroid:osmdroid-android:6.1.20'
    implementation 'com.github.MKergall:osmbonuspack:6.9.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.8.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.8.5'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    implementation 'androidx.slidingpanelayout:slidingpanelayout:1.2.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0'
    def ktor_version = '2.3.6'
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation "io.ktor:ktor-client-okhttp:$ktor_version"
    implementation "io.ktor:ktor-client-encoding:$ktor_version"
    implementation("io.nacular.measured:measured:0.4.1")
}
